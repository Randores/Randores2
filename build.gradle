buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}
plugins {
    id "net.ltgt.apt" version "0.10"
    id "com.github.johnrengelman.shadow" version "2.0.1"
    id 'com.jfrog.bintray' version '1.3.1'
}

apply plugin: 'maven-publish'
apply plugin: 'net.minecraftforge.gradle.forge'

version = "2a9"
group = "com.gmail.socraticphoenix.randores"
archivesBaseName = "Randores"

sourceCompatibility = targetCompatibility = '1.8'
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "1.12-14.21.1.2387"
    mappings = "snapshot_20170624"
    runDir = "run-client"
}

configurations {
    library
    compile.extendsFrom library
    runtime.extendsFrom library
}

repositories {
    mavenCentral()

    maven {
        name "jitpack.io"
        url "https://jitpack.io"
    }

    maven {
        url "http://tehnut.info/maven"
    }
}

dependencies {
    library "com.github.PlutoPowered:PIO:master-SNAPSHOT"
    library "com.github.PlutoPowered:Parse:master-SNAPSHOT"
    library "com.github.PlutoPowered:Collect:master-SNAPSHOT"
    library "com.github.PlutoPowered:InverseY:master-SNAPSHOT"
    library "com.github.PlutoPowered:Mirror:master-SNAPSHOT"
    library "com.github.PlutoPowered:JLSC:master-SNAPSHOT"

    shadow "mcp.mobius.waila:Hwyla:1.8.19-B33_1.12"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

//Tasks
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = "javadoc"
    from javadoc.destinationDir
}

jar {
    finalizedBy reobfJar
}

javadoc {
    destinationDir = file("docs")
}

import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

task devJar(type: ShadowJar) {
    classifier = "dev"
    from sourceSets.main.output
    configurations = [project.configurations.library]
    relocate "com.gmail.socraticphoenix.randores", "com.gmail.socraticphoenix.randores"
    relocate "com.gmail.socraticphoenix", "com.gmail.socraticphoenix.randores.lib"
}

reobf {
    shadowJar {
        mappingType = "SEARGE"
    }
}

shadowJar {
    classifier = "shadow"
    relocate "com.gmail.socraticphoenix.randores", "com.gmail.socraticphoenix.randores"
    relocate "com.gmail.socraticphoenix", "com.gmail.socraticphoenix.randores.lib"
    finalizedBy reobfShadowJar
}
//End Tasks

//Publishing
publishing {
    publications {
        maven (MavenPublication) {
            artifact jar

            artifact javadocJar {
                classifier "javadoc"
            }

            artifact shadowJar {
                classifier "shadow"
            }

            artifact devJar {
                classifier "dev"
            }

            groupId "com.gmail.socraticphoenix"
            artifactId "Randores"
            version "$version"
        }
    }

}

bintray {
    user = "meguy26"
    key = "$bintrayKey" //This is kept in gradle.properties, which is ignored by git through the .gitignore file
    publications = ["maven"]
    publish = true
    pkg {
        repo = "maven"
        name = "randores"
        licenses = ["MIT"]
        vcsUrl = "https://github.com/Randores/Randores2.git"

        version {
            name = "$version"
        }
    }
}